#!/usr/bin/env python

Import("env")

env.drivers_sources = []

# OS drivers
SConscript("unix/SCsub")
SConscript("windows/SCsub")

# Sounds drivers
if env["platform"] in ["ios", "macos"]:
    SConscript("coreaudio/SCsub")

if env["platform"] == "linuxbsd":
    if env["alsa"]:
        SConscript("alsa/SCsub")
    if env["pulseaudio"]:
        SConscript("pulseaudio/SCsub")

if env["platform"] == "windows":
    if env["xaudio2"]:
        SConscript("xaudio2/SCsub")
    SConscript("wasapi/SCsub")

# Midi drivers
SConscript("alsamidi/SCsub")
SConscript("coremidi/SCsub")
SConscript("winmidi/SCsub")

# Graphics drivers
if env["vulkan"]:
    SConscript("vulkan/SCsub")
if env["d3d12"]:
    SConscript("d3d12/SCsub")
if env["opengl3"]:
    SConscript("gl_context/SCsub")
    SConscript("gles3/SCsub")
    SConscript("egl/SCsub")

# Core dependencies
SConscript("png/SCsub")
if env["platform"] == "windows" and not env.msvc:
    SConscript("backtrace/SCsub")

env.add_source_files(env.drivers_sources, "*.cpp")

lib = env.add_library("drivers", env.drivers_sources)
env.Prepend(LIBS=[lib])
